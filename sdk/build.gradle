apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.madgag.spongycastle:core:1.52.0.0'
    compile 'com.madgag.spongycastle:prov:1.52.0.0'

    testCompile 'org.bouncycastle:bcprov-jdk15on:1.52'
    testCompile 'com.nimbusds:nimbus-jose-jwt:4.0-rc2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

javadoc {
    options.docTitle = "Worldpay Client Side Encryption (CSE) SDK"
    options.windowTitle = "Worldpay CSE SDK"
}

ext.nexusUsername = System.getenv('NEXUS_USER')
ext.nexusPassword = System.getenv('NEXUS_PASSWORD')

uploadArchives {

    def artifactVersion = System.getenv('VERSION')
    if (artifactVersion == null) {
        artifactVersion = "1.0.0-SNAPSHOT"
    }

    modifyPom {
        project {
            groupId = "com.worldpay"
            artifactId = "cse-android-sdk"
            version = artifactVersion
            packaging = 'jar'
            name = "Worldpay: CSE Android SDK"
            description = "Android SDK is a library created to help you integrate Worldpay client side encryption into your mobile applications."
            url = "http://support.worldpay.com/support/kb/gg/client-side-encryption/Content/D%20-%20Integration/Client%20Side%20Integration%20Method.htm"

            licenses {
                license {
                    name = 'Worldpay License'
                    url = "https://github.com/Worldpay/worldpay-cse-lib-android/blob/master/LICENSE"
                }
            }

            organization {
                name = "Worldpay"
                url = "https://online.worldpay.com"
            }

            developers {
                developer {
                    name = "Worldpay CSE Mobile Support"
                    email = "nicolae.ghimbovschi@worldpay.com"
                    organization = "Worldpay"
                    organizationUrl = "https://online.worldpay.com"
                }
            }

            scm {
                connection = "scm:git:git@github.com:Worldpay/worldpay-cse-lib-android.git"
                developerConnection = "scm:git:git@github.com:Worldpay/worldpay-cse-lib-android.git"
                url = "https://github.com/Worldpay/worldpay-cse-lib-android.git"
            }
        }
    }

    def signArtifacts = "TRUE".equals(System.getenv('SIGNING'))
    def publish = System.getenv('PUBLISH')

    if (signArtifacts) {
        allprojects {
            ext."signing.keyId" = System.getenv('SIGNING_KEY_ID')
            ext."signing.password" = System.getenv('SIGNING_PASSWORD')
            ext."signing.secretKeyRingFile" = System.getenv('SIGNING_KEYRING')
        }
    }

    if ("CENTRAL".equals(publish)) {
        nexus {
            sign = signArtifacts
            repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots"
        }
    } else  {
        nexus {
            sign = signArtifacts
            snapshotRepositoryUrl = "http://192.168.107.20:7000/content/repositories/snapshots/"
            repositoryUrl = "http://192.168.107.20:7000/content/repositories/releases/"
        }
    }
}